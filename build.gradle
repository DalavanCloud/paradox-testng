buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'codenarc'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

idea {
    pathVariables USER_HOME: file(System.getProperty("user.home"))
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.+'
    compile 'org.testng:testng:6.9.+'
    compile 'com.google.inject:guice:2.0'
    compile 'log4j:log4j:1.2.+'
}


group = 'com.webtrends.qa'
version = Boolean.parseBoolean(System.properties['release']) ? '3.0.7' : '4.0.0-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'wttestng'
        }
    }
    repositories { maven { url "$buildDir/repo"} }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'oss'
        name = 'paradox-testng'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/webtrends/paradox-testng'
        version {
            name = version
        }
    }
    publications = ['mavenJava']
}

if (project.hasProperty('artifactoryPublish')) {
    artifactory {
        publish {
            repository {
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishPom = true
            }
        }
    }
}

task prepareWrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

test {
    useTestNG() {
        useDefaultListeners = true
    }

    exclude '**/SystemUnderTest1.class'
    exclude '**/AnotherSuite.class'
    exclude '**/SuiteThatLogs.class'
}

codenarc {
    toolVersion = '1.0'
    configFile = new File('conf/codenarc.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 16
    maxPriority3Violations = Integer.MAX_VALUE
    reportFormat = project.properties.'codenarc.reportFormat' ?: 'html'
}
